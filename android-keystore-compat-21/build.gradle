apply plugin: 'com.android.library'
apply plugin: 'kotlin-kapt'//http://stackoverflow.com/questions/42568394/build-errors-with-new-kotlin-1-1-kapt-cant-parse-databinding-params
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

final KC_GIT_TAG_REGEX = "IMPL21KC*"
// Last Git tag name on current branch
final KC_GIT_TAG_NAME = "git describe --tags --abbrev=0 --match ${KC_GIT_TAG_REGEX}".execute([], project.rootDir).text.trim()
// Total number of tags in the app
final KC_GIT_TAG_COUNT = "git tag --list ${KC_GIT_TAG_REGEX}".execute([], project.rootDir).text.split("\n").size()

ext {
	bintrayName = 'android-keystore-compat-21'
	artifact = 'android-keystore-compat-21'

	bintrayUser = 'kotomisak'
	bintrayRepo = 'cz.koto'

	publishedGroupId = 'cz.koto'
	libraryName = 'android-keystore-compat-21'

	libraryDescription = 'KystoreCompat implementation specific for Android API 21.'

	siteUrl = 'https://github.com/kotomisak/security-showcase-android/tree/develop/keystorecompat'
	gitUrl = 'https://github.com/kotomisak/security-showcase-android'

	libraryVersion = KC_GIT_TAG_NAME.replaceAll("IMPL21KC-", "")
	libraryCode = KC_GIT_TAG_COUNT

	developerId = 'kotomisak'
	developerName = 'Michal Jenicek'
	developerEmail = 'jenicek.michal@gmail.com'

	licenseName = 'The Apache Software License, Version 2.0'
	licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	allLicenses = ["Apache-2.0"]
}


android {
	compileSdkVersion compile_sdk_version
	buildToolsVersion build_tools_version


	defaultConfig {
		minSdkVersion 21
		targetSdkVersion compile_sdk_version
		versionCode 1
		versionName "1.0"

		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	resourcePrefix 'kc_'
	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		main.java.srcDirs += 'src/debug/kotlin'
	}

}

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
		classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
	}
}


dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	api(project(':android-keystore-compat-base'))

	//implementation 'com.android.support:appcompat-v7:26.1.0'

	testImplementation 'junit:junit:4.12'
	androidTestImplementation 'com.android.support.test:runner:1.0.1'
	androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

repositories {
	mavenCentral()
}

apply from: './publish.gradle'

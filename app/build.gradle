apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// App ID base (package name)
final APPLICATION_ID = 'cz.koto.misak.securityshowcase'
final SHOWCASE_GIT_TAG_REGEX = "SHOWCASE*"
// Last Git tag name on current branch
final SHOWCASE_GIT_TAG_NAME = "git describe --tags --abbrev=0 --match ${SHOWCASE_GIT_TAG_REGEX}".execute([], project.rootDir).text.trim()
// Total number of tags in the app
final SHOWCASE_GIT_TAG_COUNT = "git tag --list ${SHOWCASE_GIT_TAG_REGEX}".execute([], project.rootDir).text.split("\n").size()

apply from: 'common.gradle'

android {
	compileSdkVersion 25
	buildToolsVersion "25.0.2"

	defaultConfig {
		applicationId APPLICATION_ID
		minSdkVersion 16
		targetSdkVersion 25
		multiDexEnabled false
		versionCode SHOWCASE_GIT_TAG_COUNT
		versionName SHOWCASE_GIT_TAG_NAME.replaceAll("SHOWCASE-", "")
		vectorDrawables.useSupportLibrary = true
	}

	buildTypes {
		release {
			shrinkResources true
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	productFlavors {
		local {

		}

		rosti {

		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		main.java.srcDirs += 'src/debug/kotlin'
		rosti.java.srcDirs += 'src/rosti/kotlin'
		local.java.srcDirs += 'src/local/kotlin'
	}

	dataBinding {
		enabled = true;
	}

	lintOptions {
		ignoreWarnings true
	}
}

kapt {
	generateStubs = true
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

	compile(project(':android-keystore-compat')) {
		exclude group: 'com.android.support'
		exclude group: 'org.jetbrains.kotlin'
	}

//	compile project(':android-keystore-compat')
	//compile(name: "android-keystore-compat-0.6.0-BETA", ext: "aar")
	compile('cz.koto.misak:android-keystore-compat:0.6.0-BETA') {
		exclude group: 'com.android.support'
	}


	final SUPPORT_LIBRARY_VERSION = "25.1.0"
	compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
	compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
	compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
	compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
	compile "com.android.support:percent:$SUPPORT_LIBRARY_VERSION"

	final RX_JAVA_VERSION = "2.0.1"
	compile "io.reactivex.rxjava2:rxjava:$RX_JAVA_VERSION"
	compile "io.reactivex.rxjava2:rxandroid:$RX_JAVA_VERSION"

	final RETROFIT_VERSION = '2.1.0'
	compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
	compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
	compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
	compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

	compile('cz.kinst.jakub:stateful-layout:1.1.5') {
		exclude group: 'com.android.support'
	}
	compile('cz.kinst.jakub:viewmodelbinding:0.9.4') {
		exclude group: 'com.android.support'
	}
	compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter:1.3.0'
	compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter-recyclerview:1.3.0'
	compile 'com.github.bumptech.glide:glide:3.7.0'
	kapt "com.android.databinding:compiler:$android_tools_version"

	final LEAK_CANARY_VERSION = '1.5'
	debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAK_CANARY_VERSION"
	releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"
	testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"


	compile 'org.parceler:parceler-api:1.1.6'
	kapt 'org.parceler:parceler:1.1.6'

//	compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.8.2@aar'
//	compile 'com.patloew.rxlocation:rxlocation:1.0.1'
//	compile 'com.mtramin:rxfingerprint:2.0.1'

	testCompile 'junit:junit:4.12'
	testCompile 'com.nhaarman:mockito-kotlin:0.7.0'
	testCompile 'org.jetbrains.spek:spek:1.0.25'
	testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
repositories {
	mavenCentral()
	maven {url "http://repository.jetbrains.com/all"}
	maven {url 'https://jitpack.io'}
	flatDir {
		dirs 'libs'
	}
}


apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'//http://stackoverflow.com/questions/42568394/build-errors-with-new-kotlin-1-1-kapt-cant-parse-databinding-params

// App ID base (package name)
final APPLICATION_ID = 'cz.koto.securityshowcase'
final SHOWCASE_GIT_TAG_REGEX = "SHOWCASE*"
// Last Git tag name on current branch
final SHOWCASE_GIT_TAG_NAME = "git describe --tags --abbrev=0 --match ${SHOWCASE_GIT_TAG_REGEX}".execute([], project.rootDir).text.trim()
// Total number of tags in the app
final SHOWCASE_GIT_TAG_COUNT = "git tag --list ${SHOWCASE_GIT_TAG_REGEX}".execute([], project.rootDir).text.split("\n").size()

apply from: 'common.gradle'

android {
	compileSdkVersion versions.sdk
	buildToolsVersion versions.build_tools

	defaultConfig {
		applicationId APPLICATION_ID
		minSdkVersion min_sdk_version
		targetSdkVersion compile_sdk_version
		multiDexEnabled false
		versionCode SHOWCASE_GIT_TAG_COUNT
		versionName SHOWCASE_GIT_TAG_NAME.replaceAll("SHOWCASE-", "")
		vectorDrawables.useSupportLibrary = true
	}

	buildTypes {
		release {
			shrinkResources true
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	flavorDimensions "lib"
	productFlavors {
		local {
			dimension = "lib"
		}

		rosti {
			dimension = "lib"
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		main.java.srcDirs += 'src/debug/kotlin'
		rosti.java.srcDirs += 'src/rosti/kotlin'
		local.java.srcDirs += 'src/local/kotlin'
	}

	dataBinding {
		enabled = true;
	}

	lintOptions {
		ignoreWarnings true
	}

	packagingOptions {
		exclude 'android/databinding/*'
	}
}

buildscript {
	repositories {
		jcenter()
	}
}

dependencies {
	//compile 'com.android.support:multidex:1.0.2'

	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

	if (project.hasProperty("keystore.compat.dependency.type")) {
		if (project.property("keystore.compat.dependency.type") == "local") {
			api(project(':android-keystore-compat')) {
				exclude group: 'com.android.support'
				exclude group: 'org.jetbrains.kotlin'
			}
			api(project(':android-keystore-compat-base')) {
				exclude group: 'com.android.support'
			}
			api(project(':android-keystore-compat-19')) {
				exclude group: 'com.android.support'
			}
			api(project(':android-keystore-compat-21')) {
				exclude group: 'com.android.support'
			}
			api(project(':android-keystore-compat-23')) {
				exclude group: 'com.android.support'
			}
		}
	} else {
		api("cz.koto:android-keystore-compat:$keystore_compat_version_latest") {
			exclude group: 'com.android.support'
		}
		api("cz.koto:android-keystore-compat-base:$keystore_compat_version_latest") {
			exclude group: 'com.android.support'
		}
		api("cz.koto:android-keystore-compat-19:$keystore_compat_version_impl") {
			exclude group: 'com.android.support'
		}
		api("cz.koto:android-keystore-compat-21:$keystore_compat_version_impl") {
			exclude group: 'com.android.support'
		}
		api("cz.koto:android-keystore-compat-23:$keystore_compat_version_impl") {
			exclude group: 'com.android.support'
		}
	}

	implementation('com.scottyab:rootbeer-lib:0.0.6') {
		exclude group: 'com.android.support'
	}

	kapt "com.android.databinding:compiler:${versions.android_databinding}"
	implementation "com.android.databinding:library:${versions.android_databinding}"
	implementation "com.android.databinding:adapters:${versions.android_databinding}"

	implementation "com.android.support:appcompat-v7:${versions.support_library}"
	implementation "com.android.support:support-annotations:${versions.support_library}"
	implementation "com.android.support:design:${versions.support_library}"
	implementation "com.android.support:cardview-v7:${versions.support_library}"
	implementation "com.android.support.constraint:constraint-layout:${versions.constraint_layout}"

	implementation "android.arch.lifecycle:extensions:${versions.lifecycle}"
	implementation "android.arch.lifecycle:common-java8:${versions.lifecycle}"


	// rxjava
	implementation("io.reactivex.rxjava2:rxandroid:$rxandroid_version") {
		exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
	}
	implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"

	// retrofit + okhttp + gson
	implementation "com.google.code.gson:gson:$gson_version"
	implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
	implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
	implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
	implementation("com.squareup.retrofit2:converter-gson:$retrofit_version") {
		transitive = true
		exclude module: 'gson'
	}
	implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
	implementation("com.squareup.retrofit2:retrofit-mock:$retrofit_version") {
		exclude group: 'com.squareup.retrofit2', module: 'retrofit'
	}

	implementation("org.alfonz:alfonz-view:$alfonz_version") {
		exclude group: 'com.android.support'
	}

	implementation ("me.tatarka.bindingcollectionadapter2:bindingcollectionadapter:$tatarka_version") {
		exclude group: 'com.android.support'
	}
	implementation ("me.tatarka.bindingcollectionadapter2:bindingcollectionadapter-recyclerview:$tatarka_version") {
		exclude group: 'com.android.support'
	}

	compile "com.github.bumptech.glide:glide:$glide_version"

// TODO use leakcanary 2.0
//	debugCompile "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
//	releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
//	testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

	implementation "org.parceler:parceler-api:$parceler_version"
	kapt "org.parceler:parceler:$parceler_version"

	implementation "com.auth0.android:jwtdecode:${versions.jwtdecode}"

	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

//	compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.8.2@aar'
//	compile 'com.patloew.rxlocation:rxlocation:1.0.1'
//	compile 'com.mtramin:rxfingerprint:2.0.1'

//	testCompile 'junit:junit:4.12'
//	testCompile 'com.nhaarman:mockito-kotlin:0.7.0'
//	testCompile 'org.jetbrains.spek:spek:1.0.25'
//	testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
repositories {
	flatDir {
		dirs 'libs'
	}
}
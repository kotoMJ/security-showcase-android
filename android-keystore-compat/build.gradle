apply plugin: 'com.android.library'
apply plugin: 'kotlin-kapt'//http://stackoverflow.com/questions/42568394/build-errors-with-new-kotlin-1-1-kapt-cant-parse-databinding-params
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

final KC_GIT_TAG_REGEX = "KC*"
// Last Git tag name on current branch
final KC_GIT_TAG_NAME = "git describe --tags --abbrev=0 --match ${KC_GIT_TAG_REGEX}".execute([], project.rootDir).text.trim()
// Total number of tags in the app
final KC_GIT_TAG_COUNT = "git tag --list ${KC_GIT_TAG_REGEX}".execute([], project.rootDir).text.split("\n").size()

ext {
	bintrayName = 'android-keystore-compat'
	artifact = 'android-keystore-compat'

	bintrayUser = 'kotomisak'
	bintrayRepo = 'cz.koto'

	publishedGroupId = 'cz.koto'
	libraryName = 'android-keystore-compat'

	libraryDescription = 'Wrapper library designed to simplify secure handling string secret in shared preferences using Keystore as private key storage.'

	siteUrl = 'https://github.com/kotomisak/security-showcase-android/tree/develop/keystorecompat'
	gitUrl = 'https://github.com/kotomisak/security-showcase-android'

	libraryVersion = KC_GIT_TAG_NAME.replaceAll("KC-", "")
	libraryCode = KC_GIT_TAG_COUNT

	developerId = 'kotomisak'
	developerName = 'Michal Jenicek'
	developerEmail = 'jenicek.michal@gmail.com'

	licenseName = 'The Apache Software License, Version 2.0'
	licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	allLicenses = ["Apache-2.0"]
}
android {
	compileSdkVersion compile_sdk_version
	buildToolsVersion build_tools_version

	defaultConfig {
		minSdkVersion 19
		targetSdkVersion compile_sdk_version
		versionCode libraryCode
		versionName libraryVersion

		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	flavorDimensions "lib"

	resourcePrefix 'kc_'
	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		main.java.srcDirs += 'src/debug/kotlin'
	}
}


buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
		classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

//	api(project(':android-keystore-compat-base'))
//	api(project(':android-keystore-compat-19'))
//	api(project(':android-keystore-compat-21'))
//	api(project(':android-keystore-compat-23'))

	api('cz.koto:android-keystore-compat-base:2.0.1') {
		exclude group: 'org.jetbrains.kotlin'
	}
	api('cz.koto:android-keystore-compat-19:2.0.1') {
		exclude group: 'org.jetbrains.kotlin'
	}
	api('cz.koto:android-keystore-compat-21:1.0.0') {
		exclude group: 'org.jetbrains.kotlin'
	}
	api('cz.koto:android-keystore-compat-23:1.0.0') {
		exclude group: 'org.jetbrains.kotlin'
	}

	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"


	androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	testCompile 'junit:junit:4.12'

}
repositories {
	mavenCentral()
}

repositories {
	flatDir {
		dirs 'libs'
	}
}

apply from: './publish.gradle'

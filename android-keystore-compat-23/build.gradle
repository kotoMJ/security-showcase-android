apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply from: "${project_directory}/lint/lint-config.gradle"

final KC_GIT_TAG_REGEX = "IMPL23KC*"
// Last Git tag name on current branch
final KC_GIT_TAG_NAME = "git describe --tags --abbrev=0 --match ${KC_GIT_TAG_REGEX}".execute([], project.rootDir).text.trim()
// Total number of tags in the app
final KC_GIT_TAG_COUNT = "git tag --list ${KC_GIT_TAG_REGEX}".execute([], project.rootDir).text.split("\n").size()

ext {
	bintrayName = 'android-keystore-compat-23'
	artifact = 'android-keystore-compat-23'

	bintrayUser = 'kotomisak'
	bintrayRepo = 'cz.koto'

	publishedGroupId = 'cz.koto'
	libraryName = 'android-keystore-compat-23'

	libraryDescription = 'KystoreCompat implementation specific for Android API 23.'

	siteUrl = 'https://github.com/kotomisak/security-showcase-android/tree/develop/keystorecompat'
	gitUrl = 'https://github.com/kotomisak/security-showcase-android'

	libraryVersion = KC_GIT_TAG_NAME.replaceAll("IMPL23KC-", "")
	libraryCode = KC_GIT_TAG_COUNT

	developerId = 'kotomisak'
	developerName = 'Michal Jenicek'
	developerEmail = 'jenicek.michal@gmail.com'

	licenseName = 'The Apache Software License, Version 2.0'
	licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	allLicenses = ["Apache-2.0"]
}

android {
	compileSdkVersion versions.sdk
	buildToolsVersion versions.build_tools


	defaultConfig {
		minSdkVersion 23
		targetSdkVersion versions.sdk
		versionCode 1
		versionName "1.0"

		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	resourcePrefix 'kc_'
	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		main.java.srcDirs += 'src/debug/kotlin'
	}
}

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
		classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	if (project.hasProperty("keystore.compat.dependency.type")) {
		if (project.property("keystore.compat.dependency.type") == "local") {
			api(project(':android-keystore-compat-base'))
		}
	} else {
		api('cz.koto:android-keystore-compat-base:2.1.0') {
			exclude group: 'org.jetbrains.kotlin'
		}
	}


	testImplementation 'junit:junit:4.12'
	androidTestImplementation "com.android.support.test:runner:${versions.test_runner}"
	androidTestImplementation "com.android.support.test.espresso:espresso-core:${versions.espresso_core}"
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
}

repositories {
	mavenCentral()
}

apply from: './publish.gradle'
